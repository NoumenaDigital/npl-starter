package objects.iou;

struct TimestampedAmount {
    amount: Number,
    timestamp: DateTime
};

@api
protocol[borrower, lender] Iou(var forAmount: Number) {
    require(forAmount > 0, "Initial amount must be strictly positive");

    initial state issued;
    state awaitingPaymentConfirmation;
    state partiallySettled;
    final state settled;
    final state cancelled;

    var amountOwed: Number = forAmount;
    var paymentToBeConfirmed: Optional<TimestampedAmount> = optionalOf<TimestampedAmount>();

    @api
    permission[borrower] pay(amount: Number) | issued, partiallySettled {
        require(amount > 0, "Amount must be strictly positive");
        require(amount <= amountOwed, "Amount may not exceed amount owed");

        paymentToBeConfirmed = optionalOf(
            TimestampedAmount(amount = amount, timestamp = now())
        );

        become awaitingPaymentConfirmation;
    };

    @api
    permission[lender] confirmPayment() | awaitingPaymentConfirmation {

        amountOwed = amountOwed - paymentToBeConfirmed.getOrFail().amount;
        paymentToBeConfirmed = optionalOf<TimestampedAmount>();

        if (amountOwed > 0) {
            become partiallySettled;
        } else {
            become settled;
        };
    };

    @api
    permission[lender] cancel() {
        become cancelled;
    };
}
